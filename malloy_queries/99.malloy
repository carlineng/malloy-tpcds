import "tpcds.malloy"

query: catalog_sales -> {
  declare:
    diff_days is cs_ship_date_sk - cs_sold_date_sk

  group_by:
    w_substr is substr(warehouse.w_warehouse_name, 1, 20)
    ship_mode.sm_type
    cc_name_lower is lower(call_center.cc_name)

  aggregate:
    days_30 is count() { where: diff_days <= 30 }
    days_31_60 is count() { where: diff_days > 30 and diff_days <= 60 }
    days_61_90 is count() { where: diff_days > 60 and diff_days <= 90 }
    days_91_120 is count() { where: diff_days > 90 and diff_days <= 120}
    days_120 is count() { where: diff_days > 120 }

  where:
    ship_date.d_month_seq >= 1200
    and ship_date.d_month_seq <= 1211
    and cs_warehouse_sk != null
    and cs_call_center_sk != null
    and cs_ship_mode_sk != null

  order_by: 
    w_substr
    sm_type
    cc_name_lower
}
