import "tpcds.malloy"

// TODO: these results have drifted, need to fix

query: all_sales -> {
  declare:
    catalog_total_sales_2001 is total_sales { where: date_dim.d_year = 2001 and channel_category = 'catalog channel'}
    catalog_total_sales_2002 is total_sales { where: date_dim.d_year = 2002 and channel_category = 'catalog channel'}
    web_total_sales_2001 is total_sales { where: date_dim.d_year = 2001 and channel_category = 'web channel'}
    web_total_sales_2002 is total_sales { where: date_dim.d_year = 2002 and channel_category = 'web channel'}
    store_total_sales_2001 is total_sales { where: date_dim.d_year = 2001 and channel_category = 'store channel'}
    store_total_sales_2002 is total_sales { where: date_dim.d_year = 2002 and channel_category = 'store channel'}

    catalog_yoy is catalog_total_sales_2002 / catalog_total_sales_2001
    store_yoy is store_total_sales_2002 / store_total_sales_2001
    web_yoy is web_total_sales_2002 / web_total_sales_2001

  group_by:
    customer.c_customer_id
    customer.c_first_name
    customer.c_last_name
    customer.c_preferred_cust_flag

  having: catalog_yoy > store_yoy
    , catalog_yoy > web_yoy
    , store_total_sales_2001 > 0
    , store_total_sales_2002 > 0
    , web_total_sales_2001 > 0
    , web_total_sales_2002 > 0
    , catalog_total_sales_2001 > 0
    , catalog_total_sales_2002 > 0

  order_by:
    c_customer_id 
    c_first_name 
    c_last_name 
    c_preferred_cust_flag 
}
