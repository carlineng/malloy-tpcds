import "tpcds.malloy"


query: daily_profit_loss_sql is duckdb.sql(
  """
  SELECT * FROM (%{ all_sales -> daily_profit_loss })
  UNION ALL
  SELECT * FROM (%{ all_returns -> daily_profit_loss })
  """
)

source: daily_profit_loss is daily_profit_loss_sql extend {
  measure: total_sales is sum(sales)
  measure: total_returns is sum(return_amt)
  measure: net_profit is sum(total_profit) - sum(net_loss)

  view: by_category is {
    group_by: channel_category
    aggregate:
      total_sales
      total_returns
      net_profit
    order_by: channel_category
  }

  view: by_channel_id is {
    group_by: channel_id
    aggregate:
      total_sales
      total_returns
      net_profit
    order_by: channel_id
    limit: 100
  }
}

run: daily_profit_loss -> {

  aggregate:
    total_sales
    total_returns
    net_profit

  nest: by_category + {
    nest: by_channel_id
    order_by: channel_category
  }

  where:
    d_date >= @2000-08-23 and d_date <= @2000-09-06
}
